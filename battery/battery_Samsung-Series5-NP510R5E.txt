#Maintained by: RehabMan for: Laptop Patches
#battery_Samsung-Series5-NP510R5E.txt
# created by PNixx 2019-10-15

# works for:
#  Samsung Series 5 NP510R5E-S05RU

into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return(Or(Arg0, ShiftLeft(Arg1, 8))) }\n
end;
into method label B1B4 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B4, 4, NotSerialized)\n
{\n
    Store(Arg3, Local0)\n
    Or(Arg2, ShiftLeft(Local0, 8), Local0)\n
    Or(Arg1, ShiftLeft(Local0, 8), Local0)\n
    Or(Arg0, ShiftLeft(Local0, 8), Local0)\n
    Return(Local0)\n
}\n
end;

# 16-bit registers
into device label H_EC code_regex BTPC,\s+16 replace_matched begin BPC0,8,BPC1,8 end;
into device label H_EC code_regex CYLC,\s+16 replace_matched begin CYC0,8,CYC1,8 end;

into method label SBIX code_regex \((\^\^PCI0\.LPCB\.H_EC\.)CYLC, replaceall_matched begin (B1B2(%1CYC0,%1CYC1), end;

into method label _BTP code_regex Store\s+\(Local0,\s+(\^\^PCI0\.LPCB\.H_EC\.)BTPC\) replaceall_matched
begin
    Store(Local0, %1BPC0)\n
    Store(ShiftRight(Local0, 8),%1BPC1)\n
end;

# 32-bit registers
into device label H_EC code_regex B1RR,\s+32 replace_matched begin B1R0,8,B1R1,8,B1R2,8,B1R3,8 end;
into device label H_EC code_regex B1PV,\s+32 replace_matched begin B1P0,8,B1P1,8,B1P2,8,B1P3,8 end;
into device label H_EC code_regex B1AF,\s+32 replace_matched begin B1A0,8,B1A1,8,B1A2,8,B1A3,8 end;
into device label H_EC code_regex B1VL,\s+32 replace_matched begin B1V0,8,B1V1,8,B1V2,8,B1V3,8 end;

into method label _BST code_regex \((\^\^PCI0\.LPCB\.H_EC\.)B1RR, replaceall_matched begin (B1B4(%1B1R0,%1B1R1,%1B1R2,%1B1R3), end;
into method label _BST code_regex \((\^\^PCI0\.LPCB\.H_EC\.)B1PV, replaceall_matched begin (B1B4(%1B1P0,%1B1P1,%1B1P2,%1B1P3), end;
into method label SBIX code_regex \((\^\^PCI0\.LPCB\.H_EC\.)B1AF, replaceall_matched begin (B1B4(%1B1A0,%1B1A1,%1B1A2,%1B1A3), end;
into method label _PTS code_regex \((\\_SB\.PCI0\.LPCB\.H_EC\.)B1AF, replaceall_matched begin (B1B4(%1B1A0,%1B1A1,%1B1A2,%1B1A3), end;
into method label _WAK code_regex \((\\_SB\.PCI0\.LPCB\.H_EC\.)B1AF, replaceall_matched begin (B1B4(%1B1A0,%1B1A1,%1B1A2,%1B1A3), end;
into method label PNOT code_regex \((\\_SB\.PCI0\.LPCB\.H_EC\.)B1AF, replaceall_matched begin (B1B4(%1B1A0,%1B1A1,%1B1A2,%1B1A3), end;
into method label SBIX code_regex \((\^\^PCI0\.LPCB\.H_EC\.)B1VL, replaceall_matched begin (B1B4(%1B1V0,%1B1V1,%1B1V2,%1B1V3), end;

into method label _BST code_regex Store\s+\(One,\s+Local0\) replaceall_matched begin Store (Zero, Local0) end;
